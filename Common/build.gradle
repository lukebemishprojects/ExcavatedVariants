plugins {
    id 'org.quiltmc.loom' version '0.12.+'
    id 'maven-publish'
}

archivesBaseName = "${mod_id}-common-${minecraft_version}"

repositories {
    maven {
        name = 'Forge / Mixin'
        url = 'https://maven.minecraftforge.net'
    }
}

loom {
    remapArchives = false
    setupRemappedVariants = false
    enableTransitiveAccessWideners = false
    runConfigs.configureEach {
        ideConfigGenerated = false
    }
    interfaceInjection {
        enableDependencyInterfaceInjection = false
    }
}

configurations {
    testCompileOnly.extendsFrom(compileOnly)
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${minecraft_version}:${parchment_version}@zip")
    }
    //DO NOT USE IN PROJECT
    //Provides necessary stuff for project to compile
    modCompileOnly("org.quiltmc:quilt-loader:${quilt_loader_version}")

    compileOnly("io.github.lukebemish.dynamic_asset_generator:dynamic_asset_generator-common-1.18.2:${rootProject.lib_version}")

    compileOnly("me.shedaniel:RoughlyEnoughItems-api:7.3.432"){transitive=false}
}

processResources {
    def buildProps = project.properties.clone()

    filesMatching(['pack.mcmeta']) {

        expand buildProps
    }
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}

remapJar.enabled = false
remapSourcesJar.enabled = false